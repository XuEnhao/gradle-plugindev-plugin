buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // of bintray plugin and plugin dev plugin
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayPluginVersion"
        classpath "nu.studer:gradle-plugindev-plugin:$plugindevPluginVersion"
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'nu.studer.plugindev'

dependencies {
    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
}

group = 'nu.studer'
version = '0.0.3.DEV'

def pomConfig = {
    name project.plugindev.pluginTitle
    description 'Gradle plugin that facilitates the bundling and uploading of Gradle plugins as expected by the Gradle Plugin portal.'
    url project.plugindev.projectUrl
    inceptionYear '2014'
    developers {
        developer {
            id 'etiennestuder'
            name project.plugindev.authorName
            email 'etienne@studer.nu'
        }
    }
    licenses {
        license {
            name 'Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.html'
        }
    }
}

plugindev {
    pluginTitle = 'Gradle plugin development plugin'
    authorName = 'Etienne Studer'
    projectUrl = 'https://github.com/etiennestuder/gradle-plugindev-plugin'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact docsJar {
                classifier "javadoc"
            }

            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

bintray {
    def versionInfo = project.version

    user = "$BINTRAY_USER"
    key = "$BINTRAY_API_KEY"

    publications = ['mavenJava']

    dryRun = true
    publish = true

    pkg {
        repo = 'gradle-plugins'
        name = 'nu.studer.plugindev'
        desc = 'Gradle plugin that facilitates the bundling and uploading of Gradle plugins as expected by the Gradle Plugin portal.'

        websiteUrl = 'https://github.com/etiennestuder/gradle-plugindev-plugin'
        issueTrackerUrl = 'https://github.com/etiennestuder/gradle-plugindev-plugin/issues'
        vcsUrl = 'https://github.com/etiennestuder/gradle-plugindev-plugin'

        licenses = ['Apache-2.0']
        labels = ['gradle', 'plugin']
        publicDownloadNumbers = true

        attributes = ['gradle-plugin': 'nu.studer.plugindev:nu.studer:gradle-plugindev-plugin']

        version {
            vcsTag = versionInfo
            attributes = ['gradle-plugin': 'nu.studer.plugindev:nu.studer:gradle-plugindev-plugin']
        }
    }
}

project.afterEvaluate {
    bintray.publications.each { pub ->
        bintrayUpload.inputs.files { ->
            project.publishing.publications.getByName(pub).publishableFiles
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
    description = 'Generates the Gradle wrapper files.'
}
